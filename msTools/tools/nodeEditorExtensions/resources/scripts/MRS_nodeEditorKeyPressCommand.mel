global proc int nodeEdKeyPressCommand(string $ned, string $key)
//
// Description:
//		This is an override of the default Node Editor `keyPressCommand`
//		Found at <drive>:\Program Files\Autodesk\Maya<version>\scripts\others\nodeEditorPanel.mel
//		The override takes control of the tab key based on the "MRS_NEExtensions_OptionVar_CNT" optionVar state
{
	string $importScript = "\n\
from msTools.coreUI.maya import inspect_utils\n\
from msTools.coreUI.maya import nodeEditor_utils\n\
from msTools.tools.nodeEditorExtensions import createNodeTool_setup";
	string $tabKeyScript = "createNodeTool_setup.install(nodeEditor_utils.getNodeEditorPageAreaFromEditor(inspect_utils.getWidget(\"^1s\")), force=True)";

	string $pwf = `getPanel -withFocus`;
	string $pup = `getPanel -underPointer`;

	if ($pup == $pwf) {
		int $mods = `getModifiers`;
		if ($mods == 0)
		{
			if ($key == "Backspace" || $key == "Del") {
				nodeEditor -e -del $ned;
				return 1;
			}
			else if ($key == "Tab") {
				if (`optionVar -q "MRS_NEExtensions_OptionVar_CNT"`) {
					python($importScript);
					python(`format -stringArg $ned $tabKeyScript`);
				}
				else
					nodeEditor -e -bcn $ned;

				return 1;
			}
		}
		else if ($mods == 1 && $key == "D") {
			if (`nodeEditor -q -inContainerView $ned`) {
				showGEPrefs;
				return 1;
			}
		}
	}
	return 0;
}